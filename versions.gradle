// build config
def bcfg = [:]
ext.bcfg = bcfg

bcfg.ver = [:]
bcfg.ver.min_sdk = 21
bcfg.ver.target_sdk = 30
bcfg.ver.build_tools = "30.0.3"
bcfg.ver.versionCode = this.createGitVersionCode()
bcfg.ver.versionName = this.createGitVersionName()

bcfg.deps = [:]
bcfg.deps.android_gradle_plugin = "com.android.tools.build:gradle:4.1.2"
bcfg.deps.android_maven_gradle_plugin = "com.github.dcendents:android-maven-gradle-plugin:2.1"
bcfg.deps.gradle_bintray_plugin = "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5"
bcfg.deps.kotlin_gradle_plugin = "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.21"
bcfg.deps.kotlin_stdlib_jdk8 = "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.21"

bcfg.deps.androidx_appcompat = "androidx.appcompat:appcompat:1.2.0"
bcfg.deps.androidx_constraintlayout = "androidx.constraintlayout:constraintlayout:2.0.4"
bcfg.deps.androidx_recyclerview = "androidx.recyclerview:recyclerview:1.1.0"
bcfg.deps.material = "com.google.android.material:material:1.3.0"

bcfg.deps.guava = "com.google.guava:guava:30.1-android"
bcfg.deps.gson = "com.google.code.gson:gson:2.8.6"

bcfg.deps.okhttp = "com.squareup.okhttp3:okhttp:4.9.0"
bcfg.deps.okhttp_logging_interceptor = "com.squareup.okhttp3:logging-interceptor:4.9.0"
bcfg.deps.retrofit = "com.squareup.retrofit2:retrofit:2.9.0"
bcfg.deps.retrofit_gson = "com.squareup.retrofit2:converter-gson:2.9.0"
bcfg.deps.retrofit_rxjava = "com.squareup.retrofit2:adapter-rxjava3:2.9.0"
bcfg.deps.leakcanary = "com.squareup.leakcanary:leakcanary-android:2.6"
bcfg.deps.javapoet = "com.squareup:javapoet:1.13.0"

bcfg.deps.rxjava = "io.reactivex.rxjava3:rxjava:3.0.10"
bcfg.deps.rxandroid = "io.reactivex.rxjava3:rxandroid:3.0.0"
bcfg.deps.rxbinding = "com.jakewharton.rxbinding4:rxbinding:4.0.0"
bcfg.deps.timber = "com.jakewharton.timber:timber:4.7.1"

bcfg.deps.fresco = "com.facebook.fresco:fresco:2.4.0"
bcfg.deps.fresco_anim_gif = "com.facebook.fresco:animated-gif:2.4.0"
bcfg.deps.fresco_anim_webp = "com.facebook.fresco:animated-webp:2.4.0"
bcfg.deps.fresco_support_webp = "com.facebook.fresco:webpsupport:2.4.0"
bcfg.deps.fresco_okhttp = "com.facebook.fresco:imagepipeline-okhttp3:2.4.0"

bcfg.deps.idonans_appcontext = "com.idonans.appcontext:appcontext:1.0.7"
bcfg.deps.idonans_core = "com.idonans.core:core:1.0.7"
bcfg.deps.idonans_lang = "com.idonans.lang:lang:1.0.3"
bcfg.deps.idonans_systeminsets = "com.idonans.systeminsets:systeminsets:1.0.1"
bcfg.deps.idonans_backstack = "com.idonans.backstack:backstack:1.0.1"
bcfg.deps.idonans_uniontype = "com.idonans.uniontype:uniontype:1.5.1"
bcfg.deps.idonans_dynamic = "com.idonans.dynamic:dynamic:1.0.3"

bcfg.addRepos = this.&addRepos

def addRepos(RepositoryHandler handler) {
    handler.jcenter()
    handler.google()
    handler.maven { url uri('./repositoryLocal') }
    handler.maven { url 'http://maven.aliyun.com/nexus/content/repositories/central/' }
    handler.maven { url 'http://maven.aliyun.com/nexus/content/repositories/releases/' }
    handler.maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    handler.maven { url 'https://dl.google.com/dl/android/maven2/' }
    handler.maven { url 'https://dl.bintray.com/idonans/maven/' }
    handler.maven { url 'https://dl.bintray.com/thelasterstar/maven/' }
    handler.maven { url 'https://dl.bintray.com/scwang90/maven' }
    handler.maven { url 'https://jitpack.io' }
}

def createGitVersionCode() {
    // 使用 git commit 总数作为 version code
    def cmd = 'git rev-list HEAD --count'
    def cmdResult = cmd.execute().text.trim().toInteger()
    def versionCodeOffset = 0
    def gitVersionCode = versionCodeOffset
    if (cmdResult) {
        gitVersionCode += cmdResult
    }

    println("gitVersionCode: " + gitVersionCode)
    return gitVersionCode
}

def createGitVersionName() {
    // 根据 git 最近的 tag 计算 version name

    // 结果形如 latestTagName-offsetCommitCount-gLatestCommitId 例如: parent-0.1-83-gbb12abc, 0.1-83-gbb12abc
    // 如果当前的 tag 就标记在最近一次的提交上, 则结果直接是 latestTagName 例如: parent-0.1, 0.1
    def cmdTagCommit = 'git describe --tags'

    def cmdTagCommitResult = cmdTagCommit.execute().text.trim()
    if (!cmdTagCommitResult) {
        println("gitVersionName: invalid cmdTagCommitResult, default to 0.0.0")
        return '0.0.0'
    }

    def cmdLatestTagCommit = 'git rev-list --tags --max-count=1'
    def cmdLatestTagCommitResult = cmdLatestTagCommit.execute().text.trim()
    if (!cmdLatestTagCommitResult) {
        println("gitVersionName: invalid cmdLatestTagCommitResult, default to 0.0.0")
        return '0.0.0'
    }

    def cmdLatestTag = 'git describe --tags ' + cmdLatestTagCommitResult
    def cmdLatestTagResult = cmdLatestTag.execute().text.trim()
    if (!cmdLatestTagResult) {
        println("gitVersionName: invalid cmdLatestTagResult, default to 0.0.0")
        return '0.0.0'
    }

    def tagCommitDesc = cmdTagCommitResult
    def latestTagName = cmdLatestTagResult
    def offsetCommitCount = 0

    def pattern = /-(\d+)-g(?!.*-g)/
    def matcher = tagCommitDesc =~ pattern

    if (latestTagName == tagCommitDesc) {
        println("latestTagName equals to tagCommitDesc, latest tag is add to latest commit, use latest tag direct.")
    } else if (matcher) {
        latestTagName = tagCommitDesc.substring(0, matcher.start())
        offsetCommitCount = matcher[0][1]
    }

    if (latestTagName != cmdLatestTagResult) {
        println("latestTagName not equals to cmdLatestTagResult, default to 0.0.0")
        return '0.0.0'
    }

    // 分析 latestTagName 语义
    def versionNamePrefix
    def versionNameSuffix
    def versionName

    println("latestTagName: " + latestTagName + ", offsetCommitCount: " + offsetCommitCount)
    // TagName                   prefix                     suffix
    // 0.1                       0.1                        .offsetCommitCount           (1)
    // parent-0.1                0.1                        .offsetCommitCount           (2)
    // parent-0.1-alpha          0.1-alpha                  offsetCommitCount            (3)
    // parent-0.1-beta           0.1-beta                   offsetCommitCount            (4)
    // parent-0.1-release        0.1                        .offsetCommitCount           (5)
    // parent-0.1-SNAPSHOT       0.1-SNAPSHOT                                            (6)
    // other                     other                      .offsetCommitCount           (7)

    // 匹配 (1) (2) (5)
    def parentReleasePattern = /^(?:parent-)?((?:\d+\.)*\d+)(?:-release)?$/
    def parentReleasePatternMatcher = latestTagName =~ parentReleasePattern
    if (parentReleasePatternMatcher) {
        versionNamePrefix = parentReleasePatternMatcher[0][1]
        versionNameSuffix = "." + offsetCommitCount
        versionName = versionNamePrefix + versionNameSuffix

        println("gitVersionName: parentReleasePatternMatcher is true, prefix: " + versionNamePrefix + ", suffix: " + versionNameSuffix + " -> " + versionName)
        return versionName
    }

    // 匹配 (3) (4)
    def parentAlphaBetaPattern = /^(?:parent-)?((?:\d+\.)*\d+(?:-alpha)?(?:-beta)?)$/
    def parentAlphaBetaPatternMatcher = latestTagName =~ parentAlphaBetaPattern
    if (parentAlphaBetaPatternMatcher) {
        versionNamePrefix = parentAlphaBetaPatternMatcher[0][1]
        versionNameSuffix = offsetCommitCount
        versionName = versionNamePrefix + versionNameSuffix

        println("gitVersionName: parentAlphaBetaPatternMatcher is true, prefix: " + versionNamePrefix + ", suffix: " + versionNameSuffix + " -> " + versionName)
        return versionName
    }

    // 匹配 (6)
    def parentSnapshotPattern = /^(?:parent-)?((?:\d+\.)*\d+(?:-SNAPSHOT)?)$/
    def parentSnapshotPatternMatcher = latestTagName =~ parentSnapshotPattern
    if (parentSnapshotPatternMatcher) {
        versionNamePrefix = parentSnapshotPatternMatcher[0][1]
        versionNameSuffix = ""
        versionName = versionNamePrefix + versionNameSuffix

        println("gitVersionName: parentSnapshotPatternMatcher is true, prefix: " + versionNamePrefix + ", suffix: " + versionNameSuffix + " -> " + versionName)
        return versionName
    }

    // 其它情况 (7)
    versionNamePrefix = latestTagName
    versionNameSuffix = "." + offsetCommitCount
    versionName = versionNamePrefix + versionNameSuffix

    println("gitVersionName: default, prefix: " + versionNamePrefix + ", suffix: " + versionNameSuffix + " -> " + versionName)
    return versionName
}
